plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'com.spendesk.grapes.compose'

    compileSdk rootProject.ext.sdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.sdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "$compose_kotlin_compiler"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release')
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // https://stackoverflow.com/a/75298544
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    api("androidx.lifecycle:lifecycle-runtime-ktx:2.6.1")

    // Compose
    api platform("androidx.compose:compose-bom:$compose_bom_version")

    api("androidx.compose.ui:ui")
    api("androidx.compose.runtime:runtime")
    // Tooling support (Previews, etc.)
    api("androidx.compose.ui:ui-tooling")
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    api("androidx.compose.foundation:foundation")
    // Material Design 2 & 3 while migration is pending.
    api("androidx.compose.material:material")
    api("androidx.compose.material3:material3:$compose_material_3_version")

    // Material design icons
    api("androidx.compose.material:material-icons-core")
    api("androidx.compose.material:material-icons-extended")
    // Integration with observables
    api("androidx.compose.runtime:runtime-rxjava2")

    // UI Tests
    androidTestImplementation("androidx.compose.ui:ui-test-junit4")
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                group = 'com.github.Spendesk'
                artifactId = 'grapes-android-compose'
                version = grapes_version

                from components.release
            }
        }
    }
}
